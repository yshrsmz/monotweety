// https://github.com/nomisRev/AndroidGradleJacoco
apply plugin: 'jacoco'

//jacoco {
//    toolVersion = "0.7.2.201409121644"
//}

//requires gradle 2.13, just became stable!! Robolectric should be fixed!!
jacoco {
  toolVersion = "0.7.9"
}
android {
  testOptions {
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
  }
}

project.afterEvaluate {
  //Gather build type and product flavor names in a list
  def buildTypes = android.buildTypes.collect { type -> type.name }
  def productFlavors = android.productFlavors.collect { flavor -> flavor.name }
  if (!productFlavors) productFlavors.add('')

  productFlavors.each { productFlavorName ->
    buildTypes.each { buildTypeName ->

      //Define the sourceName and the sourcePath.
      def sourceName, sourcePath
      if (!productFlavorName) {
        sourceName = sourcePath = "${buildTypeName}"
      } else {
        sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
        sourcePath = "${productFlavorName}/${buildTypeName}"
      }

      def testTaskName = "test${sourceName.capitalize()}UnitTest"

      //noinspection GroovyAssignabilityCheck
      task "create${sourceName.capitalize()}UnitTestCoverageReport"(type: JacocoReport, dependsOn: "$testTaskName") {

        group = "Reporting"
        description =
            "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

        reports {
          xml {
            enabled = true
            destination file("${project.buildDir}/reports/jacoco/${sourceName.capitalize()}UnitTest/jacoco-report.xml")
          }
          html {
            enabled = true
            destination file("${project.buildDir}/reports/jacoco/${sourceName.capitalize()}UnitTest/html")
          }
        }

        //Directory where the compiled class files are
        classDirectories =
            fileTree(dir: "${project.buildDir}/intermediates/classes/${sourcePath}",
                excludes: ['**/R.class',
                           '**/R$*.class',
                           '**/*$ViewInjector*.*',
                           '**/*$ViewBinder*.*',
                           '**/BuildConfig.*',
                           '**/Manifest*.*',
                           '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
                           '**/*Module.*', // Modules for Dagger.
                           '**/*Dagger*.*', // Dagger auto-generated code.
                           '**/*MembersInjector*.*', // Dagger auto-generated code.
                           '**/*_Provide*Factory*.*',
                           '**/*_Factory.*', //Dagger auto-generated code
                           '**/*$*$*.*', // Anonymous classes generated by kotlin
                           'android/databinding/*', // databinding generated code
                           '**/databinding/*Binding.*', // databinding generated code
                           '**/data/**/Models.*',
                           '**/data/**/*DBEntity.*'
                ])

        sourceDirectories = files(["src/main/java",
                                   "src/$productFlavorName/java",
                                   "src/$buildTypeName/java"])

        executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

      }
    }
  }
}